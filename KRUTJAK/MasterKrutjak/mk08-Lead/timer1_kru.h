/** KRUTJAK-ARDUINO-PRO_MINI                               *** timer1_kru.h ***
 * 
 * Модуль таймера управляющей и исполнительной систем паровозика "КРУТЯК" 
 * 
 * v3.3, 19.04.2024                                   Автор:      Труфанов В.Е.
 * Copyright © 2024 tve                               Дата создания: 12.04.2024
**/

#ifndef timer_kru
#define timer_kru
#pragma once            

#include <Arduino.h>

// Определяем флаг истечения 1 сек для запуска трассировок
volatile bool OneSecondFlag = false;

// ****************************************************************************
// *                 Инициализировать секундное первое прерывание             *
// *                        (по умолчанию с частотой в 1 Гц)                  *
// ****************************************************************************
void IniTimer1(word CalcTimer=15624)
{
   // Определяем секундное первое прерывание (с частотой в 1 Гц)
   cli();
   TCCR1A = 0;
   TCCR1B = 0; 
   TCNT1  = 0; 
   // Настраиваем регистр сравнения на частоту 1Гц
   OCR1A = CalcTimer; // 15624 = (16*10^6) / (1*1024) - 1 (must be <65536)
   // Включаем режим сравнения (CTC)
   TCCR1B |= (1 << WGM12);
   // Устанавливаем биты прескалера CS12 и CS10 на счетчик 1024
   TCCR1B |= (1 << CS12) | (1 << CS10);  
   // Включаем таймерное прерывание по сравнению
   TIMSK1 |= (1 << OCIE1A);
   sei();
} 
// ****************************************************************************
// *                         Отметить истечение 1 секунды                     *
// ****************************************************************************
ISR(TIMER1_COMPA_vect)
{
   // Устанавливаем флаг прошествия 1 секунды. Флаг всегда устанавливаем в true 
   // для того, чтобы основной цикл знал, что секунда истекла 
   // (основной цикл по своей логике и сбросит флаг в false)
   OneSecondFlag = true;
}

#endif

// ********************************************************* timer1.kru.ino ***
